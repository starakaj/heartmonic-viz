
<!DOCTYPE html>
<html lang="en">
	<head>
		<title>three.js webgl - render-to-texture</title>
		<meta charset="utf-8">
		<meta name="viewport" content="width=device-width, user-scalable=no, minimum-scale=1.0, maximum-scale=1.0">
		<style>
			body {
				color: #ffffff;
				font-family:Monospace;
				font-size:13px;
				text-align:center;
				font-weight: bold;
				background-color: #000000;
				margin: 0px;
				overflow: hidden;
			}

			#info {
				position: absolute;
				top: 0px; width: 100%;
				padding: 5px;
			}

			a {
				color: #ffffff;
			}

		</style>
	</head>
	<body>

		<div id="container"></div>
		<div id="info"><a href="http://threejs.org" target="_blank">three.js</a> render-to-texture webgl example</div>

		<script id="brush-vertex-shader" type="x-shader/x-vertex">
			precision mediump float;
			precision mediump int;
			attribute vec4 color;
			attribute float height;
			attribute vec3 vertexNormal_modelspace;
			uniform vec2 screenDimensions;
			uniform float saturation;
			uniform vec3 LightPosition_worldspace;
			varying vec3 vPosition;
			varying vec4 vColor;
			varying float vHeight;
			varying vec3 vNormal;
			varying vec3 EyeDirection_cameraspace;
			varying vec3 LightDirection_cameraspace;
			varying vec3 Normal_cameraspace;

			void main() {
				vPosition = position;
				vColor = color;
				vHeight = height;
				gl_Position = projectionMatrix * modelViewMatrix * vec4( position, 1.0 );

				vec3 Position_worldspace = (modelMatrix * vec4(position, 1)).xyz;
				vec3 vertexPosition_cameraspace = ( viewMatrix * modelMatrix * vec4(position, 1)).xyz;
				EyeDirection_cameraspace = vec3(0,0,0) - vertexPosition_cameraspace;
				vec3 LightPosition_cameraspace = ( viewMatrix * vec4(LightPosition_worldspace,1)).xyz;
				LightDirection_cameraspace = LightPosition_cameraspace + EyeDirection_cameraspace;
				Normal_cameraspace = ( viewMatrix * modelMatrix * vec4(vertexNormal_modelspace,0)).xyz; // Only correct if ModelMatrix does not scale the model ! Use its inverse transpose if not.
			}
		</script>

		<script id="brush-fragment-shader" type="x-shader/x-frag">
			precision mediump float;
			precision mediump int;

			uniform float saturation;
			uniform vec3 LightPosition_worldspace;
			uniform vec2 screenDimensions;
			uniform bool lightingEnabled;
			uniform vec3 lightColor;
			uniform float diffuseStrength;
			uniform float specularStrength;

			varying vec3 vPosition;
			varying vec4 vColor;
			varying float vHeight;
			varying vec3 EyeDirection_cameraspace;
			varying vec3 LightDirection_cameraspace;
			varying vec3 Normal_cameraspace;

			//
			// Description : Array and textureless GLSL 2D simplex noise function.
			//      Author : Ian McEwan, Ashima Arts.
			//  Maintainer : stegu
			//     Lastmod : 20110822 (ijm)
			//     License : Copyright (C) 2011 Ashima Arts. All rights reserved.
			//               Distributed under the MIT License. See LICENSE file.
			//               https://github.com/ashima/webgl-noise
			//               https://github.com/stegu/webgl-noise
			// 

			vec3 mod289(vec3 x) {
			  return x - floor(x * (1.0 / 289.0)) * 289.0;
			}

			vec2 mod289(vec2 x) {
			  return x - floor(x * (1.0 / 289.0)) * 289.0;
			}

			vec3 permute(vec3 x) {
			  return mod289(((x*34.0)+1.0)*x);
			}

			float snoise(vec2 v)
			  {
			  const vec4 C = vec4(0.211324865405187,  // (3.0-sqrt(3.0))/6.0
			                      0.366025403784439,  // 0.5*(sqrt(3.0)-1.0)
			                     -0.577350269189626,  // -1.0 + 2.0 * C.x
			                      0.024390243902439); // 1.0 / 41.0
			// First corner
			  vec2 i  = floor(v + dot(v, C.yy) );
			  vec2 x0 = v -   i + dot(i, C.xx);

			// Other corners
			  vec2 i1;
			  //i1.x = step( x0.y, x0.x ); // x0.x > x0.y ? 1.0 : 0.0
			  //i1.y = 1.0 - i1.x;
			  i1 = (x0.x > x0.y) ? vec2(1.0, 0.0) : vec2(0.0, 1.0);
			  // x0 = x0 - 0.0 + 0.0 * C.xx ;
			  // x1 = x0 - i1 + 1.0 * C.xx ;
			  // x2 = x0 - 1.0 + 2.0 * C.xx ;
			  vec4 x12 = x0.xyxy + C.xxzz;
			  x12.xy -= i1;

			// Permutations
			  i = mod289(i); // Avoid truncation effects in permutation
			  vec3 p = permute( permute( i.y + vec3(0.0, i1.y, 1.0 ))
					+ i.x + vec3(0.0, i1.x, 1.0 ));

			  vec3 m = max(0.5 - vec3(dot(x0,x0), dot(x12.xy,x12.xy), dot(x12.zw,x12.zw)), 0.0);
			  m = m*m ;
			  m = m*m ;

			// Gradients: 41 points uniformly over a line, mapped onto a diamond.
			// The ring size 17*17 = 289 is close to a multiple of 41 (41*7 = 287)

			  vec3 x = 2.0 * fract(p * C.www) - 1.0;
			  vec3 h = abs(x) - 0.5;
			  vec3 ox = floor(x + 0.5);
			  vec3 a0 = x - ox;

			// Normalise gradients implicitly by scaling m
			// Approximation of: m *= inversesqrt( a0*a0 + h*h );
			  m *= 1.79284291400159 - 0.85373472095314 * ( a0*a0 + h*h );

			// Compute final noise value at P
			  vec3 g;
			  g.x  = a0.x  * x0.x  + h.x  * x0.y;
			  g.yz = a0.yz * x12.xz + h.yz * x12.yw;
			  return 130.0 * dot(m, g);
			}

			vec3 rgb2hsv(vec3 c)
			{
				vec4 K = vec4(0.0, -1.0 / 3.0, 2.0 / 3.0, -1.0);
				vec4 p = mix(vec4(c.bg, K.wz), vec4(c.gb, K.xy), step(c.b, c.g));
				vec4 q = mix(vec4(p.xyw, c.r), vec4(c.r, p.yzx), step(p.x, c.r));

				float d = q.x - min(q.w, q.y);
				float e = 1.0e-10;
				return vec3(abs(q.z + (q.w - q.y) / (6.0 * d + e)), d / (q.x + e), q.x);
			}
			vec3 hsv2rgb(vec3 c)
			{
				vec4 K = vec4(1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0);
				vec3 p = abs(fract(c.xxx + K.xyz) * 6.0 - K.www);
				return c.z * mix(K.xxx, clamp(p - K.xxx, 0.0, 1.0), c.y);
			}

			vec3 randovec(vec2 v)
			{
				return vec3( snoise(v), snoise(vec2(v.x + 10.0, v.y)), snoise(vec2(v.x, v.y + 10.0)));
			}

			void main() {
				vec4 color = vec4( vColor );
				vec3 colorHSV = rgb2hsv( color.rgb );
				colorHSV = vec3( colorHSV.x, saturation * colorHSV.y, colorHSV.z );
				color = vec4( hsv2rgb(colorHSV), color.a );

				if (lightingEnabled) {
					float distance = length( LightPosition_worldspace - vPosition );

					vec3 n = normalize( Normal_cameraspace + 0.1 * randovec(vPosition.xy));
					vec3 l = normalize( LightDirection_cameraspace );
					float cosTheta = clamp( dot( n,l ) , 0.0, 1.0 );

					vec3 E = normalize(EyeDirection_cameraspace);
					vec3 R = reflect(-l,n);
					float cosAlpha = clamp( dot( E,R ), 0.0, 1.0 );

					color = vec4((cosTheta * color.rgb * diffuseStrength * lightColor + pow(cosAlpha, 5.0) * specularStrength * lightColor) / (distance*distance) + 0.4 * color.rgb, color.a);
				}
				gl_FragColor = color;
			}
		</script>
		<script src="../dat-gui/build/dat.gui.min.js"></script>	
		<script src="../three.js/build/three.js"></script>
		<script src="../three.js/build/Detector.js"></script>
		<script src="../three.js/build/stats.min.js"></script>
		<script type="text/javascript" src="../bezier/bezier.js"></script>
		<script src="./perlin.js"></script>
		<script src="./rgb_ryb.js"></script>
		<script src="./animation.js"></script>
		<script src="./proto-7.js"></script>
	</body>
</html>
